/*{
    "CATEGORIES": [
        "Noise",
        "Utility"
    ],
    "CREDIT": "Tuxic",
    "DESCRIPTION": "generates a basic perlin noise",
    "INPUTS": [
        {
            "DEFAULT": 12.9898,
            "MAX": 100,
            "MIN": 0,
            "NAME": "seed",
            "TYPE": "float"
        },
        {
            "DEFAULT": 4,
            "MAX": 100,
            "MIN": 0,
            "NAME": "scale",
            "TYPE": "float"
        }
    ],
    "ISFVSN": "2"
}
*/

// Author: Rich Harris
// License: MIT

// http://byteblacksmith.com/improvements-to-the-canonical-one-liner-glsl-rand-for-opengl-es-2-0/
float random(vec2 co)
{
    highp float a = seed;
    highp float b = 78.233;
    highp float c = 43758.5453;
    highp float dt= dot(co.xy ,vec2(a,b));
    highp float sn= mod(dt,3.14);
    return fract(sin(sn) * c);
}

// 2D Noise based on Morgan McGuire @morgan3d
// https://www.shadertoy.com/view/4dS3Wd

#define NUM_NOISE_OCTAVES 5

float noise (in vec2 st) {
    vec2 i = floor(st);
    vec2 f = fract(st);

    // Four corners in 2D of a tile
    float a = random(i);
    float b = random(i + vec2(1.0, 0.0));
    float c = random(i + vec2(0.0, 1.0));
    float d = random(i + vec2(1.0, 1.0));

    // Smooth Interpolation

    // Cubic Hermine Curve.  Same as SmoothStep()
    vec2 u = f*f*(3.0-2.0*f);
    u = smoothstep(0.,1.,f);

    // Mix 4 coorners porcentages
    return mix(a, b, u.x) +
            (c - a)* u.y * (1.0 - u.x) +
            (d - b) * u.x * u.y;
}

float fbm(vec2 x) {
    float v = 0.0;
    float a = 0.5;
    vec2 shift = vec2(100);
    // Rotate to reduce axial bias
    mat2 rot = mat2(cos(0.5), sin(0.5), -sin(0.5), cos(0.50));
    for (int i = 0; i < NUM_NOISE_OCTAVES; ++i) {
        v += a * noise(x);
        x = rot * x * 2.0 + shift;
        a *= 0.5;
    }
    return v;
}

void main()
{
    vec2 uv = isf_FragNormCoord.xy;
    gl_FragColor = vec4(vec3(fbm(uv * scale)), 1.0);
}
